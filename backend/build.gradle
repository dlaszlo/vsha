buildscript {
    ext.kotlin_version = "1.3.31"
    ext.spring_boot_version = "2.1.4.RELEASE"
    ext.pi4j_version = "1.2"
    ext.paho_mqtt_version = "1.2.1"
    ext.influxdb_version = "2.15"

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}

apply plugin: "application"
apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "kotlin-spring"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    api project(":frontend")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    api "org.springframework.boot:spring-boot-starter-hateoas"
    api "com.fasterxml.jackson.module:jackson-module-kotlin"
    api "org.apache.commons:commons-lang3"
    api "org.eclipse.paho:org.eclipse.paho.client.mqttv3:$paho_mqtt_version"
    api "com.jayway.jsonpath:json-path"
    api "org.influxdb:influxdb-java:$influxdb_version"
    api "com.pi4j:pi4j-core:$pi4j_version"

    testCompile group: "junit", name: "junit", version: "4.12"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    exclude "application.properties"
    exclude "logback.xml"
}

bootJar {
    mainClassName = "hu.dlaszlo.vsha.HomeAutomationKt"
}

application {
    mainClassName = "org.springframework.boot.loader.JarLauncher"

    applicationDefaultJvmArgs =
            [
                    "-Dlogging.config=MY_APP_HOME/config/logback.xml",
                    "-Dspring.config.location=MY_APP_HOME/config/application.properties",
                    "-Dlog.directory=MY_APP_HOME/log"
            ]

}

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace("MY_APP_HOME", "\$APP_HOME")
        windowsScript.text = windowsScript.text.replace("MY_APP_HOME", "%~dp0..")
    }
}
