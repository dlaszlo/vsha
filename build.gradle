import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'application'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id 'org.springframework.boot' version '2.1.3.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
}

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

group 'hu.dlaszlo'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.springframework.boot:spring-boot-starter"
    api "org.apache.commons:commons-lang3"
    api "org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.1"
    api "com.jayway.jsonpath:json-path"
    api 'io.github.rybalkinsd:kohttp:0.8.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

mainClassName = 'hu.dlaszlo.vsha.HomeAutomationKt'

jar {
    exclude 'application.properties'
    exclude 'logback.xml'
}

applicationDefaultJvmArgs =
        [
                "-Dlogging.config=MY_APP_HOME/config/logback.xml",
                "-Dspring.config.location=MY_APP_HOME/config/application.properties",
                "-Dlog.directory=MY_APP_HOME/log"
        ]

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('MY_APP_HOME', '\$APP_HOME')
        windowsScript.text = windowsScript.text.replace('MY_APP_HOME', '%~dp0..')
    }
}
